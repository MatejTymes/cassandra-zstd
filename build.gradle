// release docs: http://central.sonatype.org/pages/ossrh-guide.html

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.github.matejtymes"
archivesBaseName = "cassandra-zstd"
version = "0.2.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.cassandra', name: 'cassandra-all', version: '3.0.0'
    compile group: 'com.github.luben', name: 'zstd-jni', version: '1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Cassandra Zstandard Compressor'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Zstandard compressor for Cassandra 3.x'
                url 'https://github.com/MatejTymes/cassandra-zstd'

                scm {
                    connection 'scm:git:git://github.com/MatejTymes/cassandra-zstd.git'
                    developerConnection 'scm:git:ssh://github.com:MatejTymes/cassandra-zstd.git'
                    url 'https://github.com/MatejTymes/cassandra-zstd/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mtymes'
                        name 'Matej Tymes'
                        url 'https://github.com/MatejTymes'
                    }
                }
            }
        }
    }
}
